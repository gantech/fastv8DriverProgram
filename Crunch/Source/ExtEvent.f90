MODULE ExtEventSubs

   ! This module contains routines to calculate extreme events.

   USE                                NWTC_Library

   IMPLICIT                           NONE

CONTAINS

!=======================================================================
   SUBROUTINE EE_Init ( Fi )


      ! This routine creates the extreme-event file and initializes the extreme-event arrays.


   USE                                DataMod
   USE                                ProgGen


      ! Argument declarations.

   INTEGER, INTENT(IN)             :: Fi


      ! Local declarations.

   INTEGER                         :: Fil
   INTEGER                         :: IC
   INTEGER                         :: IE
   INTEGER                         :: IG
   INTEGER                         :: IOS

   CHARACTER(200)                  :: Frmt
   CHARACTER(100)                  :: EEFile



      ! Get date and time for extreme-event file.

   DateStr = CurDate()
   TimeStr = CurTime()


      ! Create the name for the extreme-event file.

   IF ( Aggregate )  THEN
      Fil    = 1
      EEFile = TRIM( AggRoot )//'.eev'
   ELSE
      Fil    = Fi
      EEFile = TRIM( RootName(Fi) )//'.eev'
   ENDIF


      ! Open the extreme-event file.

   CALL GetNewUnit ( EU )
   OPEN ( EU , FILE=Trim( EEFile ) , STATUS='UNKNOWN' , FORM='FORMATTED' , IOSTAT=IOS )

   IF ( IOS /= 0 )  THEN
      CALL ProgAbort ( ' Warning.  The extreme-event file "'//Trim( EEFile )//'" could not be opened.')
   ENDIF


      ! Write the appropriate header for aggregate or individual extreme-events.

   Frmt = "( / 'Extreme-events generated by ' , A , A , ' on ' , A , ' at ' , A , '.' )"
   WRITE (EU,Frmt)  TRIM( ProgName), TRIM(  ProgVer ), DateStr, TimeStr

   IF ( Aggregate )  THEN
      IF ( BadFiles == 0 )  THEN
         Frmt = "( / 'These aggregate extreme-events were based upon ' , A , ' records from ' , A" &
              //" , ' input files.' )"
         WRITE (EU,Frmt)  TRIM( Int2LStr( AnalRecs ) ), TRIM( Int2LStr( GoodFiles ) )
      ELSE
         Frmt = "( / 'These aggregate extreme-events were based upon ' , A , ' records from ' , A" &
              //" , ' of the ' , A , ' specified input files.' )"
         WRITE (EU,Frmt)  TRIM( Int2LStr( AnalRecs ) ), TRIM( Int2LStr( GoodFiles ) ), TRIM( Int2LStr( NumFiles ) )
      ENDIF
   ELSE
      Frmt = "( / 'These extreme-events for ""' , A , '"" were based upon ' , A , ' records.' )"
      WRITE (EU,Frmt)  TRIM( FileName(Fi) ), TRIM( Int2LStr( AnalRecs ) )
   ENDIF


      ! Let folks know if we used the peak finder.

   IF ( Do_PF )  THEN
     WRITE (EU,'(A)')  'The peak-finding algorithm was used.'
   ELSE
     WRITE (EU,'(A)')  'The peak-finding algorithm was not used.'
   ENDIF


      ! Write out mean wind speed and turbulence intensity.

   IF ( WS_Col > 0 )  THEN
      IF ( HaveUnits )  THEN
         WRITE (EU,'(/,A,F5.1,1X,A,/,A,F5.1,A)')  'Mean wind speed      =' , MeanWs(Fil) , Units(WS_Col) , &
                                                  'Turbulence intensity =' , TrbInt(Fil) , '%'
      ELSE
         WRITE (EU,'(/,A,F5.1,/,A,F5.1,A)')  'Mean wind speed      =' , MeanWs(Fil) , &
                                             'Turbulence intensity =' , TrbInt(Fil) , '%'
      ENDIF
   ENDIF


      ! Initialize min and max arrays.

   DO IG=1,NumEEGrps

      DO IE=1,NumEECols(IG)

         DO IC=1,NumEECols(IG)

            MaxEE(IC,IE,IG) = -Biggest
            MinEE(IC,IE,IG) = Biggest

         ENDDO ! IC

      ENDDO ! IE

   ENDDO ! IG


   RETURN
   END SUBROUTINE EE_Init ! ( Fi )
!=======================================================================
   SUBROUTINE FindEEs ( Fi )


      ! This routine generates the extreme-events.


   USE                                DataMod
   USE                                ProgGen


      ! Argument declarations.

   INTEGER, INTENT(IN)             :: Fi


      ! Local declarations.

   INTEGER                         :: Fil
   INTEGER                         :: EC
   INTEGER                         :: IC
   INTEGER                         :: IE
   INTEGER                         :: IG
   INTEGER                         :: IR



      ! Initialize the extreme-events.

   CALL EE_Init ( Fi )

   CALL WrScr ( '  Computing extreme-events.' )


      ! Are we doing aggregates?  If so, process data for all files.

   IF ( Aggregate )  THEN


         ! Let's process all the data files.

      DO Fil=1,GoodFiles


            ! Find the extremes.

         DO IR=1,NumRecs

            DO IG=1,NumEEGrps

               DO IE=1,NumEECols(IG)

                  EC = EE_Cols(IE,IG)


                     ! Do we have a new minimum for this column?

                  IF ( ConvData(EC,IR,Fil) < MinEE(IE,IE,IG) )  THEN

                     DO IC=1,TotEECols(IG)
                        MinEE(IC,IE,IG) = ConvData(EE_Cols(IC,IG),IR,Fil)
                     ENDDO ! IC

                     MinEEFile(IE,IG) = Fil

                  ENDIF


                     ! Do we have a new maximum for this column?

                  IF ( ConvData(EC,IR,Fil) > MaxEE(IE,IE,IG) )  THEN

                     DO IC=1,TotEECols(IG)
                        MaxEE(IC,IE,IG) = ConvData(EE_Cols(IC,IG),IR,Fil)
                     ENDDO ! IC

                     MaxEEFile(IE,IG) = Fil

                  ENDIF

               ENDDO ! IE

            ENDDO ! IG

         ENDDO ! IR

      ENDDO ! Fi


   ELSE


         ! Find the extremes.

      DO IR=1,NumRecs

         DO IG=1,NumEEGrps

            DO IE=1,NumEECols(IG)

               EC = EE_Cols(IE,IG)


                  ! Do we have a new minimum for this column?

               IF ( ConvData(EC,IR,Fi) < MinEE(IE,IE,IG) )  THEN

                  DO IC=1,TotEECols(IG)
                     MinEE(IC,IE,IG) = ConvData(EE_Cols(IC,IG),IR,Fi)
                  ENDDO ! IC

               ENDIF


                  ! Do we have a new maximum for this column?

               IF ( ConvData(EC,IR,Fi) > MaxEE(IE,IE,IG) )  THEN

                  DO IC=1,TotEECols(IG)
                     MaxEE(IC,IE,IG) = ConvData(EE_Cols(IC,IG),IR,Fi)
                  ENDDO ! IC

               ENDIF

            ENDDO ! IE

         ENDDO ! IG

      ENDDO ! IR

   ENDIF


      ! Output the extreme-events.

   CALL WriteEE


   RETURN
   END SUBROUTINE FindEEs ! ( Fi )
!=======================================================================
   SUBROUTINE WriteEE


      ! This routine writes the extreme-events.


   USE                             CrunchIO
   USE                             DataMod
   USE                             ProgGen

   INTEGER                      :: IC
   INTEGER                      :: IE
   INTEGER                      :: IG
   INTEGER                      :: MaxFNLen

   CHARACTER(99)                :: Frmt



      ! Loop through all the extreme-event groups.

   DO IG=1,NumEEGrps


         ! Write out the name of this group.

      WRITE (EU,'(//,A)')  'Extreme events for '//TRIM( EE_Names(IG) )//':'


         ! Write the column headings.

      IF ( Aggregate )  THEN


         IF ( TabDelim )  THEN

            IF ( HaveUnits )  THEN
               Frmt = "(/,'"//Tab//Tab//"',   ('"//Tab//"',A))"
               WRITE (Frmt( 8:10),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( TRIM( Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )
               Frmt = "('Parameter"//Tab//"Type"//Tab//"File',   ('"//Tab//"',A))"
               WRITE (Frmt(24:26),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( TRIM(  Units(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )
            ELSE
               Frmt = "('Parameter"//Tab//"Type"//Tab//"File',   ('"//Tab//"',A))"
               WRITE (Frmt(24:26),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( TRIM( Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )
            ENDIF

         ELSE


               ! Find longest file name.  Minimum length must be 4 to handle the
               ! the string "file".

            MaxFNLen = 4

            DO IC=1,NumEECols(IG)
               MaxFNLen = MAX( MaxFNLen, LEN_TRIM( Filename(MinEEFile(IC,IG)) ), LEN_TRIM( Filename(MaxEEFile(IC,IG)) ) )
            ENDDO ! IC

            IF ( HaveUnits )  THEN
               Frmt = "(/,23X,   X,   "//TextFmt//")"
               WRITE (Frmt( 8:10),'(I3)')  MaxFNLen
               WRITE (Frmt(13:15),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( ADJUSTR( Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )

               Frmt = "('  Parameter   Type     File',   X,   "//TextFmt//")"
               WRITE (Frmt(32:34),'(I3)')  MaxFNLen - 4
               WRITE (Frmt(37:39),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( ADJUSTR(  Units(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )
            ELSE
               Frmt = "('  Parameter   Type     File',   X,   "//TextFmt//")"
               WRITE (Frmt(32:34),'(I3)')  MaxFNLen - 4
               WRITE (Frmt(37:39),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( ADJUSTR(  Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )
            ENDIF

            Frmt = "('  ---------   ----     ----',   X,   "//TextFmt//")"
            WRITE (Frmt(32:34),'(I3)')  MaxFNLen - 4
            WRITE (Frmt(37:39),'(I3)')  TotEECols(IG)
            WRITE (EU,Frmt)  ( '----------', IC=1,TotEECols(IG) )

         ENDIF

      ELSE

         IF ( TabDelim )  THEN

            Frmt = "(/,'"//Tab//"',   ('"//Tab//"',A))"
            WRITE (Frmt(8:10),'(I3)')  TotEECols(IG)
            WRITE (EU,Frmt)  ( TRIM( Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )

            IF ( HaveUnits )  THEN
               Frmt = "('Parameter"//Tab//"Type',   ('"//Tab//"',A))"
               WRITE (Frmt(19:21),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( TRIM(  Units(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )
            ENDIF

         ELSE

            IF ( HAveUnits )  THEN

               Frmt = "(/,21X,   "//TextFmt//")"
               WRITE (Frmt( 8:10),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( ADJUSTR( Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )

               Frmt = "('  Parameter   Type   ',   "//TextFmt//")"
               WRITE (Frmt(26:28),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( ADJUSTR(  Units(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )

            ELSE

               Frmt = "('  Parameter   Type   ',   "//TextFmt//")"
               WRITE (Frmt(26:28),'(I3)')  TotEECols(IG)
               WRITE (EU,Frmt)  ( ADJUSTR( Titles(EE_Cols(IC,IG)) ), IC=1,TotEECols(IG) )

            ENDIF

            Frmt = "('  ---------   ----   ',   "//TextFmt//")"
            WRITE (Frmt(26:28),'(I3)')  TotEECols(IG)
            WRITE (EU,Frmt)  ( '----------', IC=1,TotEECols(IG) )

         ENDIF

      ENDIF


         ! Create the output format and write the data.

      IF ( Aggregate )  THEN

         IF ( TabDelim )  THEN
            Frmt = "(A,'"//Tab//"',A,'"//Tab//"',A,   ('"//Tab//"',"//RealFmt//",))"
            WRITE (Frmt(16:18),'(I3)')  TotEECols(IG)
         ELSE
            Frmt = "(2X,A,2X,A,2X,A,   ("//TRIM( RealFmt )//"))"
            WRITE (Frmt(18:20),'(I3)')  TotEECols(IG)
         ENDIF

         DO IE=1,NumEECols(IG)
            WRITE (EU,Frmt)  Titles(EE_Cols(IE,IG)), 'Minimum', Filename(MinEEFile(IE,IG))(:MaxFNLen), &
                             ( MinEE(IC,IE,IG), IC=1,TotEECols(IG) )
            WRITE (EU,Frmt)  Titles(EE_Cols(IE,IG)), 'Maximum', Filename(MaxEEFile(IE,IG))(:MaxFNLen), &
                             ( MaxEE(IC,IE,IG), IC=1,TotEECols(IG) )
         ENDDO ! IE

      ELSE

         IF ( TabDelim )  THEN
            Frmt = "(A,'"//Tab//"',A,   ('"//Tab//"',"//RealFmt//"))"
            WRITE (Frmt(10:12),'(I3)')  TotEECols(IG)
         ELSE
            Frmt = "(2X,A10,A9,   ("//RealFmt//"))"
            WRITE (Frmt(12:14),'(I3)')  TotEECols(IG)
         ENDIF

         DO IE=1,NumEECols(IG)
            WRITE (EU,Frmt)  Titles(EE_Cols(IE,IG)), 'Minimum', ( MinEE(IC,IE,IG), IC=1,TotEECols(IG) )
            WRITE (EU,Frmt)  Titles(EE_Cols(IE,IG)), 'Maximum', ( MaxEE(IC,IE,IG), IC=1,TotEECols(IG) )
         ENDDO ! IE

      ENDIF


   ENDDO ! IG


      ! If all of the requested input files could not be read for aggregate analyses,
      ! append the list of bad files to the output.

   IF ( Aggregate .AND. ( BadFiles > 0 ) )  CALL WrBadList ( EU )


      ! Close the extreme-event file.

   CLOSE ( EU )


   RETURN
   END SUBROUTINE WriteEE
!=======================================================================

END MODULE ExtEventSubs
